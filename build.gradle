/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.3/userguide/tutorial_java_projects.html
 */

// Apply the java plugin to add support for Java
plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'io.ratpack.ratpack-java' version '1.4.5'
}

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Doesn't matter, only so that spring doesn't fail
mainClassName = 'me.philippheuer.twitch4j.TwitchClient'

// Method, that gets the total amount of commits in the repository (as build number)
def gitVersion() {
	def counter = 0
	def process = "git rev-list dev --first-parent --count".execute()
	counter = process.text.toInteger()
	return counter
}

// Dependencies
dependencies {
	// Logging
    compile("ch.qos.logback:logback-classic:1.2.1")
    compile("com.jcabi:jcabi-log:0.17.2")

    // Spring (RestTemplate)
    compile("org.springframework:spring-web:4.3.6.RELEASE")
	compile("org.springframework.social:spring-social-core:1.1.4.RELEASE")

    // WebSocket Client for Twitch PubSub
    compile('com.neovisionaries:nv-websocket-client:1.31')

    // IRC Client [https://github.com/KittehOrg/KittehIRCClientLib]
    compile('org.kitteh.irc:client-lib:3.0.2');

    // JSON / YAML
    compile("com.fasterxml.jackson.core:jackson-databind:2.8.7")

	// Bucket for rate-limiting
	compile("org.isomorphism:token-bucket:1.7")

    // Expiring Map
    compile("net.jodah:expiringmap:0.5.7")

    // TypeTools
    compile("net.jodah:typetools:0.4.8")

	// Args4J - CommandLineArgumentParser
	compile("args4j:args4j:2.33")

    // Getter & Setter
    compileOnly("org.projectlombok:lombok:1.16.14")
}

// Disable Gradle Caching
configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

/**
 * Sources - build a jar with source files
 */
task sourcesJar(type: Jar) {
	from sourceSets.main.java.srcDirs
	classifier = 'sources'
}

/**
 * JavaDocs Generation
 */
javadoc {
	source = sourceSets.main.allJava
	classpath = sourceSets.main.compileClasspath

	// if not changed the destinationDir, the javadoc for production code is overridden.
	destinationDir = file("${buildDir}/../docs/javadoc")
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

/**
 * Artifacts
 */
artifacts {
	archives sourcesJar
	archives javadocJar
}

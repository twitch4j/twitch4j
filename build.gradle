// Plugins
plugins {
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.jfrog.artifactory' version '4.13.0'
}

// Artifact Info
project.ext {
	groupId = 'com.github.twitch4j'
	artifactNamespace = 'twitch4j'
	artifactName = 'Twitch4J'
	artifactVersion = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "")
	artifactDescription = 'Twitch4J'
	websiteUrl = 'https://github.com/twitch4j/twitch4j'
	issueTrackerUrl = 'https://github.com/twitch4j/twitch4j/issues'
	vcsUrl = 'https://github.com/twitch4j/twitch4j.git'
}

// All-Projects
allprojects {
	// Repositories
	repositories {
		jcenter()
		maven { url "https://maven.google.com" }
		mavenLocal()
	}
}

// Subprojects
subprojects {
    apply plugin: 'java'
	apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'com.jfrog.artifactory'

    // Source Compatiblity
    sourceCompatibility = 1.8

    // Dependency Management for Subprojects
    dependencyManagement {
        dependencies {
            // Logging
            dependency group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
            dependency group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

            // Apache Commons
            dependency group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
            dependency group: 'commons-io', name: 'commons-io', version: '2.6'
			dependency group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
			dependency group: 'commons-configuration', name: 'commons-configuration', version: '1.10'

			// Event Dispatcher
 			dependency group: 'com.github.philippheuer.events4j', name: 'events4j-core', version: '0.7.1'
			dependency group: 'com.github.philippheuer.events4j', name: 'events4j-handler-simple', version: '0.7.1'
			
			// Credential Manager
			dependency group: 'com.github.philippheuer.credentialmanager', name: 'credentialmanager', version: '0.1.0'

			// HTTP Client
            dependency group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.3.0'

			// Rate Limiting
			dependency group: 'com.github.vladimir-bukhtoyarov', name: 'bucket4j-core', version: '4.7.0'

			// Http Client
            dependency group: 'io.github.openfeign', name: 'feign-okhttp', version: '10.7.0'
            dependency group: 'io.github.openfeign', name: 'feign-jackson', version: '10.7.0'
            dependency group: 'io.github.openfeign', name: 'feign-slf4j', version: '10.7.0'
			dependency group: 'io.github.openfeign', name: 'feign-hystrix', version: '10.7.0'

			// Hystrix
			dependency group: 'com.netflix.hystrix', name: 'hystrix-core', version: '1.5.18'

			// Jackson (JSON)
            dependency group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
			dependency group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: '2.10.1'
			dependency group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.10.1'
			dependency group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.10.1'

			// WebSocket
			dependency group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.9'

			// Annotations
			dependency group: 'org.jetbrains', name: 'annotations', version: '18.0.0'

            // Testing
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.2'
			dependency group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.2'

            // Lombok
            dependency group: 'org.projectlombok', name: 'lombok', version: '1.18.2'
        }
    }

	// Source encoding
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	// Common Dependencies
	plugins.withType(JavaPlugin) {
		dependencies {
			// Annotation processors
			compileOnly group: 'org.projectlombok', name: 'lombok'
			annotationProcessor group: 'org.projectlombok', name: 'lombok'
			testCompileOnly group: 'org.projectlombok', name: 'lombok'
			testAnnotationProcessor group: 'org.projectlombok', name: 'lombok'

			// Apache Commons
			compile group: 'org.apache.commons', name: 'commons-lang3'
			compile group: 'commons-io', name: 'commons-io'

			// Logging
			compile group: 'org.slf4j', name: 'slf4j-api'
			testCompile group: 'ch.qos.logback', name: 'logback-classic'

			// Test
			testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api'
			testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
		}
	}
}

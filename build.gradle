// Apply the java plugin to add support for Java
plugins {
	id 'idea'
	id 'eclipse'
	id 'net.ltgt.apt' version '0.15'
	id 'net.ltgt.apt-idea' version '0.15'
	id 'com.jfrog.bintray' version '1.8.1'
	id 'net.ltgt.apt-eclipse' version '0.15'
	id 'pl.allegro.tech.build.axion-release' version '1.9.2'
	id "com.gorylenko.gradle-git-properties" version '1.5.1'
	id 'io.spring.dependency-management' version '1.0.5.RELEASE'
}

allprojects {
	apply plugin: 'io.spring.dependency-management'

	ext {
		projectUrl = 'https://github.com/twitch4j/twitch4j'
		gitUrl = 'https://github.com/twitch4j/twitch4j.git'
		url = projectUrl
	}

	dependencyManagement {
		// BOMs
		imports {
			mavenBom 'com.fasterxml.jackson:jackson-bom:2.9.6'
			mavenBom 'io.projectreactor:reactor-bom:Bismuth-SR10'
			mavenBom 'org.springframework:spring-framework-bom:5.0.6.RELEASE'
		}
		// Twitch4J
		dependencies {
			// Logging
			dependency group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

			// Apache Commons
			dependency group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'

			// Spring
			// - WebFlux
			dependency group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.0.2.RELEASE'
			// - Social
			dependency group: 'org.springframework.social', name: 'spring-social-core', version: '1.1.6.RELEASE'

			// REST
			// - OkHttp
			dependency group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.10.0'
			// - Spring

			// WebSockets
			dependency group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.3.8'

			// Annotations
			// - Nullable
			dependency group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
			// - Getter & Setter
			dependency group: 'org.projectlombok', name: 'lombok', version: '1.18.0'

			// Utilities
			// - EventManager
			dependency group: 'com.github.PhilippHeuer', name: 'events4j', version: 'v0.2.0'
			// - Expiring Map
			dependency group: 'net.jodah', name: 'expiringmap', version: '0.5.8'
			// - Immutables
			dependency group: 'org.immutables', name: 'value', version: '2.6.1'
			// - Streams
			dependency group: 'com.github.twitch4j', name: 'stream4j', version: 'master-SNAPSHOT'

			// Testing
			dependency group: 'junit', name: 'junit', version: '4.12'
			dependency group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
			dependency group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
		}
	}

	/**
	 * Repositories
	 */
	repositories {
		jcenter()
		maven { url "https://jitpack.io" }
	}

	scmVersion {
		useHighestVersion = true
		tag {
			prefix = 'v'
			versionSeparator = ''
		}
		createReleaseCommit true // should create empty commit to annotate release in commit history, false by default
		releaseCommitMessage { version, position -> "chore(RELEASE): Release ${version}" } // custom commit message if commits are created
	}

	// Artifact Info
	group = 'com.github.twitch4j'
	description = 'This is a api client for the Twitch API V5/IRC/PubSub. (And related services: Streamlabs, ...)'
	// Acquire for deploying using Travis CI
	project.version = scmVersion.version

	configurations.all {
		// Disable Gradle Caching
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
	}
}

subprojects {
	apply plugin: 'java'

	apply from: "${rootDir}/test.gradle"
	apply from: "${rootDir}/deploy.gradle"

	sourceSets {
		main {
			java {
				srcDirs = ['src/main/java']
			}
		}
	}

	// Dependencies
	dependencies {
		// Logging
		compile group: 'org.slf4j', name: 'slf4j-api'

		// Annotations
		// - Nullable
		compile group: 'com.google.code.findbugs', name: 'jsr305'
		// - Getter & Setter
		compileOnly group: 'org.projectlombok', name: 'lombok'
		annotationProcessor group: 'org.projectlombok', name: 'lombok'
		testCompile group: 'org.projectlombok', name: 'lombok'

		// Jackson
		compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
		compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'

		// Testing
		testCompile group: 'junit', name: 'junit'
		testCompile group: 'io.projectreactor', name: 'reactor-test'
		testCompile group: 'org.mockito', name: 'mockito-core'
		testCompile group: 'ch.qos.logback', name: 'logback-classic'
	}
}

wrapper {
	gradleVersion = "4.8" // newer version
	distributionType = Wrapper.DistributionType.ALL // distribution type ALL
}
